/*
 * Copyright (c) 2022, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 Inc. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
import org.apache.tools.ant.taskdefs.condition.Os
plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs'
}

description = 'Ballerina - persist-tools Tooling Test'

def buildDir = "build";
def ballerinaDist = "$project.buildDir/jballerina-tools-${ballerinaLangVersion}"

configurations {
    balTools
    dist {
        transitive true
    }
}

dependencies {
    checkstyle project(':checkstyle')
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstylePluginVersion}"

    testImplementation "org.ballerinalang:ballerina-lang:${ballerinaLangVersion}"
    testImplementation "org.ballerinalang:ballerina-cli:${ballerinaLangVersion}"
    testImplementation group: 'info.picocli', name: 'picocli', version: '4.7.1'

    balTools ("org.ballerinalang:jballerina-tools:${ballerinaLangVersion}") {
        transitive = false
    }

    testImplementation project(":persist-cli")
    testImplementation group: 'org.testng', name: 'testng', version: "${testngVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation (group: 'mysql', name: 'mysql-connector-java', version: "${mySqlDriverVersion}") {
        transitive = false
    }
    testImplementation (group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: "${mssqlDriverVersion}") {
        transitive = false
    }
    testImplementation (group: 'org.postgresql', name: 'postgresql', version: "${postgresqlDriverVersion}") {
        transitive = false
    }
}

clean {
    delete "$project.projectDir/build"
    delete "$project.rootDir/target"
}

task jBallerinaPack {
    doLast {
        configurations.balTools.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            copy {
                from project.zipTree(artifact.getFile())
                into new File("${project.buildDir}")
            }
            copy {
                from(project.zipTree(artifact.getFile())) {
                    eachFile { fcd ->
                        fcd.relativePath = new RelativePath(!fcd.file.isDirectory(), fcd.relativePath.segments.drop(1))
                    }
                    includeEmptyDirs = false
                }
                into "${project.rootDir}/target/ballerina-distribution"
            }
        }
    }
    outputs.dir ballerinaDist
}

task unpackStdLibs() {
    dependsOn(jBallerinaPack)
    doLast {
        configurations.ballerinaStdLibs.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            copy {
                from project.zipTree(artifact.getFile())
                into new File("${project.buildDir}/extracted-stdlibs/" + artifact.name + "-zip")
            }
        }
    }
}

task pullGSheetsDependency() {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            return exec {
                ignoreExitValue true
                String distributionBinPath = "${project.rootDir}/target/ballerina-distribution/bin"
                commandLine 'sh', '-c', "$distributionBinPath/bal pull ballerinax/googleapis.sheets"
            }.exitValue
        } else {
            return exec {
                ignoreExitValue true
                String distributionBinPath = "${project.rootDir}/target/ballerina-distribution/bin"
                commandLine 'cmd', '/c', "$distributionBinPath/bal.bat pull ballerinax/googleapis.sheets"
            }.exitValue
        }
    }
}

task copyStdlibs() {
    dependsOn(unpackStdLibs)

    /* Standard Libraries */
    doLast {
        configurations.ballerinaStdLibs.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            def artifactExtractedPath = "${project.buildDir}/extracted-stdlibs/" + artifact.name + "-zip"
            def persistToolBala = "${project.rootDir}/persist-tool/build/bala_unzipped/bala"
            copy {
                into ballerinaDist
                into("repo/bala") {
                    from "${artifactExtractedPath}/bala"
                }
                into("repo/cache") {
                    from "${artifactExtractedPath}/cache"
                }
            }
            copy {
                def runtimePath = "${project.rootDir}/target/ballerina-distribution"
                into runtimePath
                into("repo/bala") {
                    from "${artifactExtractedPath}/bala"
                }
                into("repo/cache") {
                    from "${artifactExtractedPath}/cache"
                }
            }
            copy {
                def testDistribution = "${project.rootDir}/persist-cli-tests/build/ballerina-distribution"
                into testDistribution
                into("repo/bala") {
                    from "${persistToolBala}"
                }
            }
        }
    }
}

tasks.withType(Checkstyle) {
    exclude '**/module-info.java'
}

task copyTestResources(type: Copy) {
    into buildDir
    into("generated-sources/") {
        from "src/test/resources/test-src/input"
    }
    doNotTrackState("build needs to run every time")
}

task copyTestPack(type: Copy) {
    dependsOn(copyStdlibs)
    dependsOn(copyTestResources)
    into buildDir
    into("ballerina-distribution/") {
        from ballerinaDist
    }
    doNotTrackState("build needs to run every time")
}

def checkExecResult(executionResult, failText, standardOutput) {
    if (executionResult) {
        Provider<ExecResult> execResultProvider = executionResult.getProvider()
        int exitCode = execResultProvider.get().getExitValue()
        if (exitCode != 0) {
            throw new GradleException('Non-zero exit value: ' + exitCode)
        }
        if (standardOutput.toString().contains(failText)) {
            throw new GradleException('"' + failText + '" string in output: ' + standardOutput.toString())
        }
    } else {
        throw new GradleException('Returned a null execResult object')
    }
}

def checkTestDockerContainerStatus(dockerName) {
    if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
        try {
            return exec {
                commandLine 'sh', '-c',
                        "docker exec ${dockerName} mysqladmin ping -hlocalhost -uroot -pTest123# --silent"
            }.exitValue
        } catch (all) {
            return 1
        }
    }
}

task startTestDockerContainer(type: Exec) {
    if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
        def standardOutput = new ByteArrayOutputStream()
        commandLine 'sh', '-c', 'docker run --platform linux/amd64 --rm -d --name ballerina-persist-tools -e MYSQL_ROOT_PASSWORD="Test123#" -p 3307:3306 -t mysql:8.0.21'
        def healthCheck = 1
        def counter = 0
        doLast {
            checkExecResult(executionResult, 'Error', standardOutput)
            while (healthCheck != 0 && counter < 12) {
                sleep(30 * 1000)
                healthCheck = checkTestDockerContainerStatus('ballerina-persist-tools')
                counter = counter + 1
            }
            if (healthCheck != 0) {
                throw new GradleException("Docker container 'ballerina-mysql' health test exceeded timeout!")
            }
        }
    } else {
        commandLine 'cmd','slmgr /dlv'
        println('\n' + "Windows Instance detected")
        println('\n' + "skippped starting the docker container")
    }
}

task stopTestDockerContainer() {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            try {
                def stdOut = new ByteArrayOutputStream()
                exec {
                    commandLine 'sh', '-c', 'docker stop ballerina-persist-tools'
                    standardOutput = stdOut
                }
            } catch (all) {
                println('Gradle process can safely ignore stopTestDockerContainer task')
            }
        }
    }
}

task startMSSQLTestDockerContainer(type: Exec) {
    if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
        def standardOutput = new ByteArrayOutputStream()
        commandLine 'sh', '-c', 'docker run --platform linux/amd64 --rm -d --name ballerina-persist-tools-mssql -e ACCEPT_EULA=1 -e SA_PASSWORD=Test123# -p 1434:1433 -t mcr.microsoft.com/mssql/server:2019-latest'
        def healthCheck = 1
        def counter = 0
        doLast {
            checkExecResult(executionResult, 'Error', standardOutput)
            while (healthCheck != 0 && counter < 12) {
                sleep(30 * 1000)
                healthCheck = checkMSSQLTestDockerContainerStatus('ballerina-persist-tools-mssql')
                counter = counter + 1
            }
            if (healthCheck != 0) {
                throw new GradleException("Docker container 'ballerina-persist-tools-mssql' health test exceeded timeout!")
            }
        }
    } else {
        commandLine 'cmd','slmgr /dlv'
        println('\n' + "Windows Instance detected")
        println('\n' + "skippped starting the docker container")
    }
}

def checkMSSQLTestDockerContainerStatus(containerName) {
    if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
        try {
            return exec {
                commandLine 'sh', '-c',
                        "docker exec ${containerName} /opt/mssql-tools18/bin/sqlcmd -C -S localhost -U sa -P Test123#"
            }.exitValue
        } catch (all) {
            return 1;
        }
    }
}

task stopMSSQLTestDockerContainer() {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            try {
                def stdOut = new ByteArrayOutputStream()
                exec {
                    commandLine 'sh', '-c', "docker stop ballerina-persist-tools-mssql"
                    standardOutput = stdOut
                }
            } catch (all) {
                println("Process can safely ignore stopMSSQLTestDockerContainer task")
            }
        }
    }
}

task startPostgreSQLTestDockerContainer(type: Exec) {
    if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
        def standardOutput = new ByteArrayOutputStream()
        commandLine 'sh', '-c', 'docker run --platform linux/amd64 --rm -d --name ballerina-persist-tools-postgresql -e POSTGRES_USER=postgres -e POSTGRES_PASSWORD=postgres -p 5432:5432 -d postgres'
        def healthCheck = 1;
        def counter = 0;
        doLast {
            checkExecResult(executionResult, 'Error', standardOutput)
            while (healthCheck != 0 && counter < 12) {
                sleep(30 * 1000)
                healthCheck = checkPostgreSQLTestDockerContainerStatus("ballerina-persist-tools-postgresql")
                counter = counter + 1;
            }
            if (healthCheck != 0) {
                throw new GradleException("Docker container 'ballerina-persist-tools-postgresql' health test exceeded timeout!")
            }
        }
    } else {
        commandLine 'cmd','slmgr /dlv'
        println('\n' + "Windows Instance detected")
        println('\n' + "skipped starting the docker container")
    }
}

def checkPostgreSQLTestDockerContainerStatus(containerName) {
    if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
        try {
            return exec {
                commandLine 'sh', '-c', "docker exec ${containerName} psql -U postgres -h localhost -p 5432"
            }.exitValue
        } catch (all) {
            return 1;
        }
    }
}

task stopPostgreSQLTestDockerContainer() {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            try {
                def stdOut = new ByteArrayOutputStream()
                exec {
                    commandLine 'sh', '-c', "docker stop ballerina-persist-tools-postgresql"
                    standardOutput = stdOut
                }
            } catch (all) {
                println("Process can safely ignore stopPostgreSQLTestDockerContainer task")
            }
        }
    }
}

checkstyle {
    toolVersion "${project.checkstylePluginVersion}"
    configFile rootProject.file("build-config/checkstyle/build/checkstyle.xml")
    configProperties = ["suppressionFile" : file("${rootDir}/build-config/checkstyle/build/suppressions.xml")]
}

checkstyleTest.dependsOn(":checkstyle:downloadCheckstyleRuleFiles")
checkstyleTest.dependsOn(":persist-cli-tests:copyTestPack")
checkstyleMain.dependsOn(":persist-cli-tests:copyTestPack")
spotbugsTest.dependsOn(":persist-cli-tests:copyTestPack")
spotbugsMain.dependsOn(":persist-cli-tests:copyTestPack")
jar.dependsOn(":persist-cli-tests:copyTestPack")
compileTestJava.dependsOn(":persist-cli-tests:copyTestPack")

spotbugsTest {
    def classLoader = plugins["com.github.spotbugs"].class.classLoader
    def SpotBugsConfidence = classLoader.findLoadedClass("com.github.spotbugs.snom.Confidence")
    def SpotBugsEffort = classLoader.findLoadedClass("com.github.spotbugs.snom.Effort")
    effort = SpotBugsEffort.MAX
    reportLevel = SpotBugsConfidence.LOW
    reportsDir = file("$project.buildDir/reports/spotbugs")
    reports {
        html.enabled true
        text.enabled = true
    }
    def excludeFile = file("${rootDir}/spotbugs-exclude.xml")
    if(excludeFile.exists()) {
        excludeFilter = excludeFile
    }
}

spotbugsMain {
    enabled false
}

checkstyleMain {
    enabled false
}

test {
    dependsOn(copyTestPack)
    systemProperty "ballerina.home", ballerinaDist
    dependsOn(copyTestResources)
    systemProperty "ballerina.offline.flag", "true"
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
    testLogging.showStandardStreams = true
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "${jacocoVersion}"
}

jacocoTestReport {
    dependsOn test
    def classFiles = new File("${rootDir}/persist-cli/build/classes/java/main/io/ballerina/persist")
    additionalClassDirs(classFiles)
    reports {
        xml.required = true
    }
}

test.dependsOn startTestDockerContainer
test.dependsOn startMSSQLTestDockerContainer
test.dependsOn startPostgreSQLTestDockerContainer
test.dependsOn pullGSheetsDependency
test.dependsOn ":persist-cli:build"
test.finalizedBy stopTestDockerContainer
test.finalizedBy stopMSSQLTestDockerContainer
test.finalizedBy stopPostgreSQLTestDockerContainer

build.doNotTrackState("build needs to run every time")
build.dependsOn ":persist-cli:build"
build.dependsOn copyTestPack
